buildscript {
    def springRepo = 'https://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

ext.moduleName = 'com.apress.cems.boot.test'

//override any Spring Boot configurations, so this version of JUnit can be used
//ext['junit-jupiter.version'] = "5.5.0"

dependencies {
    api boot_bom.starterWeb, boot_bom.starterJpa
    api (misc_bom.ojdbc) {
        exclude group: "com.oracle.database.jdbc", module: "ucp"
    }

    testImplementation testing_bom.api, boot_bom.starterTest, testing_bom.platformRunner, misc_bom.h2
    testRuntimeOnly testing_bom.engine, testing_bom.platformRunner
}

test {

    useJUnitPlatform()

    systemProperties['junit.jupiter.execution.parallel.enabled'] = true
    systemProperties['junit.jupiter.execution.parallel.config.strategy'] = 'dynamic'
    systemProperties['junit.jupiter.execution.parallel.mode.default'] = 'concurrent'
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 1

    reports {
        //junitXml.enabled(true)
        junitXml.required = true
        //html.enabled(true)
        html.required = true
    }

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

// de-comment this after leaving a single main class to build jar
jar {
    archiveBaseName = "sample-boot"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 03 - Spring Boot Testing",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.boot.Application",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

