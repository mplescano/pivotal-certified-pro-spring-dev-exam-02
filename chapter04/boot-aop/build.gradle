plugins {
    id 'java-library'
    id "bom-conventions"
}

ext.moduleName = 'com.apress.cems.boot.test'

dependencies {
    implementation boot_bom.starterWeb, boot_bom.starterJpa, misc_bom.lang3
    implementation (misc_bom.ojdbc) {
        exclude group: "com.oracle.database.jdbc", module: "ucp"
    }

    testImplementation testing_bom.api, boot_bom.starterTest, testing_bom.platformRunner, misc_bom.h2
    testImplementation testing_bom.api
    testRuntimeOnly testing_bom.engine, testing_bom.platformRunner
}

test {

    useJUnitPlatform()

    systemProperties['junit.jupiter.execution.parallel.enabled'] = true
    systemProperties['junit.jupiter.execution.parallel.config.strategy'] = 'dynamic'
    systemProperties['junit.jupiter.execution.parallel.mode.default'] = 'concurrent'
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 1

    reports {
        junitXml.required = true
        html.required = true
    }

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jar {
    archiveBaseName = "sample-boot"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 04 - Spring Boot Aop",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.aop.Application",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

