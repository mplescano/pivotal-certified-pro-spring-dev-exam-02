buildscript {
    def springRepo = 'https://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

ext.moduleName = 'com.apress.cems.boot'

dependencies {
    implementation boot_bom.starterSecurity, boot_bom.starterJpa, boot_bom.starterThymeleaf, boot_bom.starterWeb, misc_bom.thymeleafSecurity, "org.springframework.boot:spring-boot-starter-validation"
    runtimeOnly misc_bom.h2
    implementation boot_bom.actuator

    testImplementation spring_bom.securityTest
    testImplementation (testing_bom.restAssured) {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    }
    testImplementation (boot_bom.starterTest) {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

// decomment this after leaving a single main class to build jar
jar {
    archiveBaseName = "boot-sec"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 07 - Spring Boot Security with Thymeleaf",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.BootSecureApp",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

