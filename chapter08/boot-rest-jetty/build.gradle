buildscript {
    def springRepo = 'https://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

ext.moduleName = 'com.apress.cems.rest'

dependencies {
    implementation boot_bom.starterJpa, boot_bom.starterJetty, "org.springframework.boot:spring-boot-starter-validation"
    implementation (boot_bom.starterWeb){
        exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
    }
    runtimeOnly misc_bom.h2
    implementation boot_bom.actuator

    testImplementation (testing_bom.restAssured) {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    }
    testImplementation (boot_bom.starterTest) {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jar {
    archiveBaseName = "boot-rest-jetty"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 08 - Spring Boot REST on Jetty",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.RestApplication",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

