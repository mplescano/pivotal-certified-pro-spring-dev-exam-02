buildscript {
    repositories {
        mavenCentral()
    }
}

subprojects {
    group 'com.apress.cems'
    if (!project.name.contains("boot")) {
        apply plugin: 'java'
        apply plugin: 'idea'

        dependencies {
            implementation project(':chapter00:dao')
            implementation misc_bom.lang3, misc_bom.slf4j, misc_bom.jsr250
            implementation (misc_bom.ojdbc) {
                exclude group: "com.oracle.database.jdbc", module: "ucp"
            }
            implementation (misc_bom.logback) {
                exclude group: "javax.activation", module: "activation"
            }
            compileOnly misc_bom.lombok
            annotationProcessor misc_bom.lombok

            testCompileOnly misc_bom.lombok
            testAnnotationProcessor misc_bom.lombok
            testImplementation misc_bom.h2
        }

        //because Gradle is an idiot
        test {
            exclude 'module-info.class'
            useJUnitPlatform()

            testLogging {
                events 'PASSED', 'FAILED', 'SKIPPED'
            }
        }

        afterEvaluate {
            repositories {
                mavenCentral()
            }

            compileJava {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                            '-proc:none',
                            '--module-path', classpath.asPath,
                    ]
                    classpath = files()
                }
            }
        }
    }
}