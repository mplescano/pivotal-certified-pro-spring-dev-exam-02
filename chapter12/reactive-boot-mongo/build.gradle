buildscript {
    def springRepo = 'https://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation boot_bom.starterMongoReactive, boot_bom.starterWebflux, boot_bom.actuator
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation testing_bom.reactorTest, testing_bom.mongo
    testImplementation (boot_bom.starterTest) {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

// decomment this after leaving a single main class to build jar
jar {
    archiveBaseName = "reactive-boot-mongo"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 12 - Spring Boot Webflux Application - fully reactive with MongoDB",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.reactive.ReactiveApplication",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

