buildscript {
    def springRepo = 'https://repo.spring.io'
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$springRepo/release" }
        maven { url "$springRepo/snapshot" }
        maven { url "$springRepo/milestone" }
    }

    dependencies {
        classpath boot.springBootPlugin
    }
}

apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    implementation boot_bom.starterJpa, boot_bom.starterWeb, boot_bom.actuator, boot_bom.prometheus, "org.springframework.boot:spring-boot-starter-validation"
    runtimeOnly misc_bom.h2

    testImplementation (testing_bom.restAssured) {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    }
    testImplementation (boot_bom.starterTest) {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "junit", module: "junit"
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}


jar {
    archiveBaseName = "prometheus-boot-app"
    manifest {
        attributes(
                "Created-By": System.getProperty('java.version'),
                "Specification-Title": "Pivotal Certified Professional Spring Developer Exam - Chapter 09 - Spring Boot REST",
                "Implementation-Version": archiveVersion,
                "Main-Class"         : "com.apress.cems.PrometheusApplication",
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

